apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

artifacts {
    archives androidSourcesJar
}

version = VERSION_NAME
group = GROUP

afterEvaluate { project ->
    signing {
        sign publishing.publications
    }
    publishing {
        repositories {
            maven {
//                url = uri("$rootDir/repo")
                url = project.properties.get("RELEASE_REPOSITORY_URL") ?: ""

                credentials(PasswordCredentials) {
                    username = project.properties.get("NEXUS_USERNAME") ?: ""
                    password = project.properties.get("NEXUS_PASSWORD") ?: ""
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release

                groupId = GROUP
                version = VERSION_NAME

                artifact androidSourcesJar

                pom {
                    artifactId archivesBaseName
                    name = POM_NAME
                    packaging = POM_PACKAGING
                    description = POM_DESCRIPTION
                    url = POM_URL

                    scm {
                        url = POM_SCM_URL
                        connection = POM_SCM_CONNECTION
                        developerConnection = POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name = POM_LICENCE_NAME
                            url = POM_LICENCE_URL
                            distribution = POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            id = POM_DEVELOPER_ID
                            name = POM_DEVELOPER_NAME
                            email = POM_DEVELOPER_MAIL
                        }
                    }
                }
            }
        }
    }
}